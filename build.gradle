buildscript {
	repositories {
		jcenter()

		maven {
			url "http://files.minecraftforge.net/maven"
		}
	}

	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:2.0.0"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "1.12.2-1.21.1.6"
group = "com.therandomlabs.randompatches"
archivesBaseName = "randompatches"

repositories {
	maven {
		url "https://jitpack.io"
	}
}

dependencies {
	shadow group: "com.github.TheRandomLabs", name: "RandomLib", version: "1.12-SNAPSHOT", changing: true
	compile group: "com.github.TheRandomLabs", name: "RandomLib", version: "1.12-SNAPSHOT", changing: true
}

shadowJar {
	classifier = ""
	relocate "com.therandomlabs.randomlib", "com.therandomlabs.randompatches.repackage.com.therandomlabs.randomlib"
	configurations = [project.configurations.shadow]
}

reobf {
	shadowJar {
		mappingType = "SEARGE"
	}
}

build.dependsOn shadowJar

artifacts {
	archives shadowJar
}

minecraft {
	version = "1.12.2-14.23.5.2847"
	runDir = "run"
	mappings = "stable_39"

	replace "@VERSION@", project.version
	replace "@FINGERPRINT@", project.findProperty("signSHA1")
}

processResources {
	//Ensure this task is redone when the versions change
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	//Replace version and mcversion in mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
}

jar {
	manifest {
		attributes "Manifest-Version": "1.0"
		attributes "FMLAT": "randompatches_at.cfg"
		attributes "FMLCorePlugin": "com.therandomlabs.randompatches.core.RPCore"
	}
}

task signJar(type: SignJar, dependsOn: reobfJar) {
	onlyIf {
		project.hasProperty("keyStore")
	}

	keyStore = project.findProperty("keyStore")
	alias = project.findProperty("keyStoreAlias")
	storePass = project.findProperty("keyStorePass")
	keyPass = project.findProperty("keyStoreKeyPass")
	inputFile = jar.archivePath
	outputFile = jar.archivePath
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = "deobf"
}

artifacts {
	archives deobfJar
}

build.dependsOn signJar
