apply plugin: "java-library"

version = "2.2.1-forge"
group = "com.therandomlabs.randompatches"
archivesBaseName = "randompatches"

ext {
	commonGradleBranch = "master"
	useMixin = true
	minecraftVersion = "1.16.5"
	forgeVersion = "36.0.4"
	mappingsChannel = "snapshot"
	mappingsVersion = "20210126-mixed-1.16.5"
	clothConfigVersion = "4.1.3"
	autoConfigVersion = "3.0"
	autoConfigTOMLVersion = "autoconfig-3.x.x-forge-SNAPSHOT"
}

buildscript {
	repositories {
		jcenter()

		mavenCentral()

		gradlePluginPortal()

		maven {
			url "http://files.minecraftforge.net/maven"
		}

		maven {
			url "https://dist.creeper.host/Sponge/maven"
		}
	}

	dependencies {
		classpath group: "net.minecraftforge.gradle", name: "ForgeGradle",
				version: "3.+", changing: true
		classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
		classpath "gradle.plugin.com.modrinth.minotaur:Minotaur:1.1.0"
		classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
	}
}

apply from: "https://raw.githubusercontent.com/TheRandomLabs/Common-Gradle/${project.commonGradleBranch}/forge.gradle"

if (project.hasProperty("curseForgeAPIKey")) {
	curseforge {
		apiKey = project.curseForgeAPIKey

		project {
			id = "285612"

			addGameVersion "Forge"
			addGameVersion "Java 10"
			addGameVersion "Java 9"
			addGameVersion "Java 8"
			addGameVersion "1.16.5"
			addGameVersion "1.16.4"
			addGameVersion "1.16.3"
			addGameVersion "1.16.2"
			addGameVersion "1.16.1"

			mainArtifact(jar) {
				changelogType = "markdown"
				changelog = file("changelog.md")
				displayName = "RandomPatches ${version}"
				releaseType = "beta"

				relations {
					optionalDependency "cloth-config-forge"
				}
			}

			addArtifact(deobfJar) {
				displayName = "RandomPatches ${version} Deobf"
				releaseType = "release"
			}

			addArtifact(sourcesJar) {
				displayName = "RandomPatches ${version} Sources"
				releaseType = "release"
			}

			addArtifact(javadocJar) {
				displayName = "RandomPatches ${version} Javadoc"
				releaseType = "release"
			}
		}
	}

	tasks.curseforge.dependsOn build
}

if (project.hasProperty("modrinthToken")) {
	task publishModrinth(type: com.modrinth.minotaur.TaskModrinthUpload) {
		token = project.modrinthToken
		projectId = "JmtW1Cr5"
		versionNumber = version
		versionName = "RandomPatches ${version}"
		changelog = new File("changelog.md").getText()
		uploadFile = jar
		releaseType = "beta"
		addGameVersion("1.16.5")
		addGameVersion("1.16.4")
		addGameVersion("1.16.3")
		addGameVersion("1.16.2")
		addGameVersion("1.16.1")
		addLoader("forge")
		addFile(deobfJar)
		addFile(sourcesJar)
		addFile(javadocJar)
	}
}
